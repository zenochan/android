apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'maven'

repositories {
  maven { url "https://jitpack.io" }

  // 个推 http://docs.getui.com/mobile/android/androidstudio_maven/
  maven { url "http://mvn.gt.igexin.com/nexus/content/repositories/releases/" }

  // 阿里百川 HotFix
  maven { url "http://repo.baichuan-android.taobao.com/content/groups/BaichuanRepositories" }
}

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.2"

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  dataBinding { enabled = true }

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 25
    versionCode 1
    versionName "1.0.1"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  testCompile 'junit:junit:4.12';
  provided fileTree(include: ['*.jar'], dir: 'libs');

  /**************************************
   *              框架性依赖             *
   **************************************/

  //support 家族
  //https://developer.android.com/topic/libraries/support-library/features.html
  compile 'com.android.support:support-annotations:25.3.1'
  compile 'com.android.support:support-v4:25.3.1';
  compile 'com.android.support:appcompat-v7:25.3.1'
  compile 'com.android.support:cardview-v7:25.3.1';
  compile 'com.android.support:gridlayout-v7:25.3.1'
  compile 'com.android.support:design:25.3.1';
  compile 'com.android.support:multidex:1.0.1'
  compile 'com.android.support:percent:25.3.1'
  compile 'com.android.support.constraint:constraint-layout:1.0.2'
  provided 'com.android.support:palette-v7:25.3.1';

  provided 'com.belerweb:pinyin4j:2.5.1'

  //Logger https://github.com/orhanobut/logger
  compile "com.orhanobut:logger:1.15";

  //Glide https://github.com/bumptech/glide
  compile "com.github.bumptech.glide:glide:3.7.0";

  //FastJson https://github.com/alibaba/fastjson
  compile "com.alibaba:fastjson:1.1.56.android";

  //https://github.com/JakeWharton/butterknife
  compile 'com.jakewharton:butterknife:8.5.1'
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

  //Retrofit http://square.github.io/retrofit/
  compile "com.squareup.retrofit2:retrofit:2.2.0";

  // rx 家族
  compile "io.reactivex.rxjava2:rxjava:2.0.7"
  compile "io.reactivex.rxjava2:rxandroid:2.0.1"
  //https://github.com/JakeWharton/retrofit2-rxjava2-adapter
  compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
  compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.3@aar'

//  compile 'com.github.tianzhijiexian:CommonAdapter:1.2.2'


  compile 'com.squareup:otto:1.3.8';
  compile 'org.projectlombok:lombok:1.16.16';
  // 个推 http://docs.getui.com/mobile/android/androidstudio/
  provided 'com.getui:sdk:2.10.2.1'

  /**************************************
   *              功能性依赖             *
   **************************************/
  // 友盟分析 http://mvnrepository.com/artifact/com.umeng.analytics/analytics
  provided 'com.umeng.analytics:analytics:6.0.9'
  provided('com.taobao.android:alisdk-hotfix:1.4.0') { transitive = false }

  /**************************************
   *              控件性依赖             *
   **************************************/
  //  https://github.com/afollestad/material-dialogs
  compile "com.afollestad.material-dialogs:core:0.9.4.4"
  compile "com.afollestad.material-dialogs:commons:0.9.4.4"

  compile 'me.zhanghai.android.materialprogressbar:library:1.1.7'

  //BottomBar https://github.com/roughike/BottomBar
  compile 'com.roughike:bottom-bar:2.3.0'

  //时间选择 120kb
  compile 'com.wdullaer:materialdatetimepicker:2.5.0'
}


uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file:///Users/zenochan/.m2/repository")

      pom.groupId = "name.zeno"
      pom.artifactId = 'android'
      pom.version = android.defaultConfig.versionName

      pom.project {
        name 'android'
        packaging 'aar'
        description 'Zeno\' Android App Dev Kit'
        url 'https://github.com/zenochan/android'

        scm {
          url 'https://github.com/zenochan/android'
          connection 'https://github.com/zenochan/android'
          developerConnection 'https://zenochan.github.io'
        }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'Apache-2.0'
          }
        }

        developers {
          developer {
            id 'zenochan'
            name 'Zeno Chan'
            email 'zenochan@qq.com'
          }
        }
      }
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar
  archives sourcesJar
}
